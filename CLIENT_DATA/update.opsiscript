; This sourcecode published under the Terms of the General Public License.
; Written by Eric Esser, Wissenschaftszentrum Berlin, eric.esser@wzb.eu.
; Inspired by uib gmbh (www.uib.de), Thomas Besser (KIT), Detlef Krummel (GEI) and Tobias Friede (Fraunhofer)

[Actions]
;==========================================================================================================
; Includes
include_insert "common.opsiinc"

;==========================================================================================================
; Variables 
DefVar $CurrentRegistryUpdateVersion$
DefVar $VersionComparisonResult$


;==========================================================================================================
; Program logic

comment "Start updating Endnote"
Message "Updating Endnote to version " + $ProgramVersion$

; Find the current update version of the acutal base program, e.g. Endnote X7.x
Set $CurrentRegistryUpdateVersion$ = GetRegistryStringValue("[HKEY_LOCAL_MACHINE\SOFTWARE\Classes\Installer\Products\" + $RegistryInstallerClass$ + "\] Version")
Set $VersionComparisonResult$ = CompareDotSeparatedNumbers($CurrentRegistryUpdateVersion$, $DesiredRegistryUpdateVersion$)

; If the desired update version is higher as the current one, make the update, otherwise skip it.
if ($VersionComparisonResult$ = "-1") 
  if (not($ComboUpdatePatch$ = ""))
    Winbatch_update
    sub_check_exitcode
  else
    comment "No update patch in opsi package defined, cancelling update"      
    LogWarning "No update patch in opsi package defined, cancelling update" 
  endif 
else
  comment "Endnote already up to date, canceling update"
endif

; Include post deinstall file in custom folder via OPSI property 
comment "include custom post install file"
set $CustomPostInstall$ = getProductProperty("custom-post-install","none")
if not ($CustomPostInstall$ = "none")
  if FileExists("%ScriptPath%\custom\" + $CustomPostInstall$)
    include_insert "%ScriptPath%\custom\" + $CustomPostInstall$
  endif
endif


;==========================================================================================================
; Functions

[Winbatch_update]
msiexec /update "%ScriptPath%\files\$ComboUpdatePatch$" /L*v "$LogDir$\$ProductId$.install_log.txt" /passive /norestart ALLUSERS=1 REBOOT=ReallySuppress
